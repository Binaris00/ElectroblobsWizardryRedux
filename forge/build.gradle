plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev.legacyforge'
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
}

mixin {
    add(sourceSets.main, "${mod_id}.refmap.json")

    config("${mod_id}.mixins.json")
    config("${mod_id}.forge.mixins.json")
}

legacyForge {
    version = "${minecraft_version}-${forge_version}"

    validateAccessTransformers = true

    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers = ["src/main/resources/META-INF/accesstransformer.cfg"]
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        client {
            client()
        }

        data {
            programArguments = ['--mod', mod_id, '--all', '--output',
                                rootProject.file('common/src/generated/resources/').getAbsolutePath(),
                                '--existing', rootProject.file('common/src/main/resources/').getAbsolutePath()]
            data()
        }

        server {
            server()
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'forge.enabledGameTestNamespaces', mod_id
        }
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

repositories {
    maven { url 'https://maven.wispforest.io/releases' }
    maven { url 'https://maven.su5ed.dev/releases' }
    maven { url = "https://maven.shedaniel.me/" }
}

dependencies {
    compileOnly project(":common")
    compileOnly 'org.jetbrains:annotations:23.0.0'
    compileOnly "io.github.llamalad7:mixinextras-common:${mixinextras_version}"
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:${mixinextras_version}"))
    annotationProcessor "io.github.llamalad7:mixinextras-common:${mixinextras_version}"
    annotationProcessor("org.spongepowered:mixin:0.8.5-SNAPSHOT:processor")

    modCompileOnly "io.wispforest:accessories-neoforge:${project.accessories_version}"
    modImplementation("me.shedaniel.cloth:cloth-config-forge:${project.cloth_config_version}") {
        exclude group: 'net.fabricmc', module: 'fabric-loader'
    }
}

sourceSets.main.resources {
    srcDir project(":common").file('src/generated/resources').getAbsolutePath()
    exclude '.cache'
}

jar {
    finalizedBy('reobfJar')
    manifest.attributes([
            "MixinConfigs": "${mod_id}.mixins.json,${mod_id}.forge.mixins.json"
    ])
}

test {
    useJUnitPlatform()
}

publishMods {
    file = tasks.named("reobfJar").flatMap { it.archiveFile }
    changelog = rootProject.file("CHANGELOG.md").text
    type = STABLE
    displayName = "[Forge] EbWizardry-Redux ${project.version} for ${minecraft_version}"
    modLoaders.add("forge")

    curseforge {
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        projectId = "1405790"
        minecraftVersions.add("1.20.1")

        optional { slug = "accessories" }
    }

    modrinth {
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = "s5qOekOc"
        minecraftVersions.add("1.20.1")

        optional { slug = "accessories" }
    }
}
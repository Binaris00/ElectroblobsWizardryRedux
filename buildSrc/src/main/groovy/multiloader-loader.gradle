plugins {
    id 'multiloader-common'
}

// Append the loader suffix (project name) to the project version for loader subprojects
// e.g. version 0.7.1.1 -> 0.7.1.1-fabric or 0.7.1.1-forge
// Avoid double-appending if already present.
afterEvaluate {
    try {
        def loaderSuffix = project.name?.toString()
        if (loaderSuffix) {
            def ver = project.version?.toString() ?: ''
            if (ver && !ver.endsWith("-${loaderSuffix}")) {
                project.version = "${ver}-${loaderSuffix}"
            }
        }
    } catch (Exception ignored) {
        // fail silently; don't break configuration for unusual setups
    }
}

configurations {
    commonJava{
        canBeResolved = true
    }
    commonResources{
        canBeResolved = true
    }
}

dependencies {
    compileOnly(project(':common')) {
        capabilities {
            requireCapability "$group:$mod_id"
        }
    }
    commonJava project(path: ':common', configuration: 'commonJava')
    commonResources project(path: ':common', configuration: 'commonResources')
}

tasks.named('compileJava', JavaCompile) {
    dependsOn(configurations.commonJava)
    source(configurations.commonJava)
}

processResources {
    dependsOn(configurations.commonResources)
    from(configurations.commonResources)
}

tasks.named('javadoc', Javadoc).configure {
    dependsOn(configurations.commonJava)
    source(configurations.commonJava)
}

tasks.named('sourcesJar', Jar) {
    dependsOn(configurations.commonJava)
    from(configurations.commonJava)
    dependsOn(configurations.commonResources)
    from(configurations.commonResources)
}
